PRINT "  LOADING GAME", , ,"PLEASE WAIT FOR WELCOME SCREEN"
100 REM init
110 SETCOLOR 1,1,14: SETCOLOR 2,0,0: SETCOLOR 4,0,0
120 PRINT CHR$(125): POKE 752,1 : PRINT CHR$(32)
130 DX = 0: DY = 0 : DT = 0 : S = 15 : SLAST = 15 : T = 1 : TLAST = 1
140 X = 3: Y = 3
150 DIM LITCAND$(1), UNLITCAND$(1), PCHAR$(1) : LITCAND$ = CHR$(42): UNLITCAND$ = CHR$(46)

200 SIZE = 7: CURX = 1: CURY = 1: SCX = 4: SCY = 4
990 GOTO 32000

1000 REM joystick reading
1010 S = STICK(0) : T = STRIG(0) : SLAST = S : TLAST = T
1020 S = STICK(0) : T = STRIG(0) : IF S = SLAST AND T = TLAST THEN 1020 
1030 SLAST = S : IF S = 15 AND T = TLAST THEN 1020
1040 IF TLAST <> T AND T = 1 THEN TLAST = T: GOTO 1020
1050 DX = (S = 5 OR S = 6 OR S = 7) - (S = 9 OR S = 10 OR S = 11)
1060 DY = (S = 5 OR S = 9 OR S = 13) - (S = 6 OR S = 10 OR S = 14)
1070 DT = ABS(T - 1)
1080 RETURN

10000 REM Display candle
10020 IF BOARD(CURX, CURY) = 1 THEN PCHAR$ = LITCAND$ : GOTO 10110
10030 PCHAR$ = UNLITCAND$
10110 POSITION 8+(CURX * 3), -1+CURY * 3: PRINT PCHAR$
10200 RETURN

10500 REM Display selection mark
10510 IF SCY <> CURY OR SCX <> CURX THEN GOTO 10600
10520 POSITION 7+(CURX * 3), -2+CURY * 3: PRINT CHR$(9)
10530 POSITION 9+(CURX * 3), -2+CURY * 3: PRINT CHR$(15)
10540 POSITION 7+(CURX * 3), CURY * 3: PRINT CHR$(11)
10550 POSITION 9+(CURX * 3), CURY * 3: PRINT CHR$(12)
10560 RETURN
10600 POSITION 7+(CURX * 3), -2+CURY * 3: PRINT CHR$(32)
10610 POSITION 9+(CURX * 3), -2+CURY * 3: PRINT CHR$(32)
10620 POSITION 7+(CURX * 3), CURY * 3: PRINT CHR$(32)
10630 POSITION 9+(CURX * 3), CURY * 3: PRINT CHR$(32)
10640 RETURN

11000 REM Display whole board
11005 PRINT CHR$(125): REM Clear screen
11010 FOR Y=1 TO SIZE: FOR X=1 TO SIZE: CURX=X: CURY=Y
11020 GOSUB 10000: REM Display candle
11030 NEXT X: NEXT Y
11040 CURY = SCY: CURX = SCX: GOSUB 10500: REM Display selection mark
11050 RETURN

12000 REM Toggle Current Candle
12010 BOARD(CURX, CURY) = ABS(BOARD(CURX, CURY) - 1)
12020 IF SCX < SIZE THEN BOARD(CURX + 1, CURY) = ABS(BOARD(CURX + 1, CURY) - 1)
12030 IF SCX > 1 THEN BOARD(CURX - 1, CURY) = ABS(BOARD(CURX - 1, CURY) - 1)
12040 IF SCY < SIZE THEN BOARD(CURX, CURY + 1) = ABS(BOARD(CURX, CURY + 1) - 1)
12050 IF SCY > 1 THEN BOARD(CURX, CURY - 1) = ABS(BOARD(CURX, CURY - 1) - 1)
12100 GOSUB 10000: REM Update candles
12110 IF SCX > 1 THEN CURY = SCY: CURX = SCX - 1: GOSUB 10000
12120 IF SCY > 1 THEN CURY = SCY - 1: CURX = SCX: GOSUB 10000
12130 IF SCY < SIZE THEN CURY = SCY + 1: CURX = SCX: GOSUB 10000
12140 IF SCX < SIZE THEN CURY = SCY: CURX = SCX + 1: GOSUB 10000
12200 RETURN

13000 REM Initialize board
13010 SIZE = 7: DIM BOARD(SIZE, SIZE)
13100 FOR Y=1 TO SIZE: FOR X=1 TO SIZE: BOARD(X, Y) = 0: NEXT X: NEXT Y
13110 RETURN

30000 REM Main loop - point movement
30010 POSITION X, Y: PRINT CHR$(32)
30020 X = X + DX: Y = Y + DY: POSITION X, Y: PRINT CHR$(CHAR)
30030 GOSUB 1000
30040 IF DT AND CHAR = 20 THEN CHAR = 148: GOTO 30060
30050 IF DT AND CHAR = 148 THEN CHAR = 20
30060 GOTO 30000

31000 REM Main loop - candles game
31001 GOSUB 13000: REM Init empty board
31002 GOSUB 11000: REM Display whole board
31100 CURX = SCX: CURY = SCY
31110 GOSUB 1000: REM Read Joystick
31120 IF DT THEN GOSUB 12000: REM Toggle candle
31130 SCX = SCX + DX : SCY = SCY + DY
31140 GOSUB 10500: REM Update old candle
31150 CURX = SCX: CURY = SCY: GOSUB 10500: REM Update new candle
31300 GOTO 31100

32000 REM Welcome screen
32005 PRINT CHR$(125): REM Clear screen
32010 PRINT "", "WELCOME TO CANDLES"
32020 PRINT ""
32030 PRINT "THE GOAL OF THIS GAME IS TO LIGHT UP"
32031 PRINT "ALL THE CANDLES"
32040 PRINT "CHANGING ONE CANDLE WILL AFFECT"
32041 PRINT "ALL ADJACENT ONES"
32050 PRINT ""
32060 PRINT LITCAND$;" - CANDLE LIT"
32070 PRINT UNLITCAND$;" - CANDLE DOWN"
32080 PRINT ""
32090 PRINT "PRESS FIRE TO START"
32100 GOSUB 1000: REM Read Joystick
32110 IF DT THEN GOTO 31000: REM Main game
32120 GOTO 32100